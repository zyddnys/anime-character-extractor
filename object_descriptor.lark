start: config_statement object_statement postprocess_statement

?postprocess_statement: "postprocess" "{" postprocess* "}"
?postprocess: POSTPROCESS_TAG [postprocess_match_regex | postprocess_match_tag | postprocess_match_category | postprocess_match_prob | postprocess_match_exact | postprocess_match_any] [postprocess_action_drop | postprocess_action_append] ";"
?postprocess_match_regex: regex_symbol DOUBLE_QUOTED_STRING
?postprocess_match_category: category_symbol CNAME
?postprocess_match_prob: prob_symbol NUMBER
?postprocess_match_exact: exact_symbol TAG
?postprocess_match_any: any_symbol
?postprocess_match_tag: tag_symbol query
?postprocess_action_drop: drop_symbol TAG*
?postprocess_action_append: append_symbol TAG+

?config_statement: "configs" "{" config+ "}"
?config: TAG "=" (DOUBLE_QUOTED_STRING | NUMBER) ";"

?object_statement: "objects" "{" object+ "}"
?object: TAG "=" query ";"

?query: or_expr
?or_expr: and_expr ("or" and_expr)*
?and_expr: not_expr ("and" not_expr | not_expr)*
?not_expr: not_symbol* atom
?atom: "(" or_expr ")" | single_tag
?single_tag: TAG COMPARE NUMBER
?not_symbol: "NOT"

?regex_symbol: "regex"
?category_symbol: "category"
?prob_symbol: "prob"
?exact_symbol: "exact"
?any_symbol: "any"
?tag_symbol: "tag"
?drop_symbol: "drop"
?append_symbol: "append"

COMPARE: /[<>]/
TAG: /[\*\w\d\_\-]+[\w\d\_\-\(\)]*/
POSTPROCESS_TAG: /[\*\w\d\_]+/
DOUBLE_QUOTED_STRING: /"[^"]*"/

COMMENT: /\/\/.*\n/

%import common.CNAME
%import common.NUMBER
%import common.WS
%ignore WS
%ignore COMMENT
